subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    defaultTasks 'jar'

    group = 'jp.hishidama.utility'
    version = '0.1.0'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    def defaultEncoding = 'UTF-8'
    [compileJava, compileTestJava]*.options*.encoding = defaultEncoding

    repositories {
        mavenCentral()
        maven { url 'http://hishidama.github.io/mvnrepository' }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    task wrapper(type: Wrapper) {
        gradleVersion '2.8'
        jarFile file('.buildtools/gradlew.jar')
    }

    eclipse.classpath.file {
        whenMerged { classpath ->
            classpath.entries.findAll { entry -> entry.kind == 'output' }*.path = 'classes'
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
        repositories {
            maven {
                url "$buildDir/../../../../mvnrepository"
            }
        }
    }
}
